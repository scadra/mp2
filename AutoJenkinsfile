def archivedArtifact = 'marketplace-front-SNAPSHOT-bin.zip'
def securityAuditFile = 'securityAudit.txt'
def codeLintReportFile = 'codeLintReport.txt'

pipeline {

    agent any

    options {
        disableConcurrentBuilds()
        buildDiscarder(logRotator(daysToKeepStr: '15', numToKeepStr: '5'))
    }

    triggers {
        gitlab(triggerOnPush: true, branchFilterType: 'All')
    }

    stages {
        stage('Dependencies') {
            options {
                timeout(time: 20, unit: 'MINUTES')
            }
            tools {
                nodejs "nodejs-14.15.0"
            }
            steps {
                sh 'git status'
                sh 'node -v'
                sh 'npm --version'
                sh 'npm install'
            }
        }

        stage('Build') {
            options {
                timeout(time: 10, unit: 'MINUTES')
            }
            tools {
                nodejs "nodejs-14.15.0"
            }
            steps {
                sh 'npm run build'
                zip dir: 'dist', zipFile: archivedArtifact, archive: true
            }
        }

        stage('QA') {
            options {
                timeout(time: 5, unit: 'MINUTES')
            }
            tools {
                nodejs "nodejs-14.15.0"
            }
            steps {
                echo 'Dependency check for vulnerabilities'
                tee(securityAuditFile) {
                    catchError(buildResult: 'UNSTABLE', stageResult: 'FAILURE') {
                        sh 'npm audit --audit-level=high'
                    }
                }
                echo 'Source code quality check'
                tee(codeLintReportFile) {
                    catchError(buildResult: 'UNSTABLE', stageResult: 'FAILURE') {
                        sh 'npm run lint'
                    }
                }
            }
        }
    }

    post {
        unsuccessful {
            script {
                def commitMessage = sh (script: 'git log -1 --pretty=%B', returnStdout: true).trim()
                def commitAuthor = sh (script: 'git log -1 --pretty=%aN', returnStdout: true).trim()
                def commitEmail = sh (script: 'git log -1 --pretty=%aE', returnStdout: true).trim()
                def commitHash = sh (script: 'git log -1 --pretty=%h', returnStdout: true).trim()
                def securityAudit = readFile securityAuditFile
                def codeLintReport = readFile codeLintReportFile

                mail subject: "${currentBuild.currentResult}: ${env.JOB_BASE_NAME}/${env.BRANCH_NAME}#${env.BUILD_NUMBER}",
                        from: 'Jenkins <jenkins@luxhub.com>',
                        to: commitEmail,
                        mimeType: 'text/html',
                        body: """
                            <html><body>
                            <h1>Job ${env.JOB_BASE_NAME}: ${currentBuild.currentResult}</h1>
                            <p>This is the build <strong>#${env.BUILD_NUMBER}</strong> on branch
                                <strong>${env.BRANCH_NAME}</strong> run by the job <strong>${env.JOB_NAME}</strong>.</p>
                            <p>Please <a href="${env.BUILD_URL}console">check the job execution log here</a>.</p>
                            <hr/>
                            <p>Commit <strong>${commitHash}</strong> by <strong>${commitAuthor}</strong>: </p>
                            <pre>${commitMessage}</pre>
                            <hr/>
                            <p>Security audit report: </p>
                            <pre>${securityAudit}</pre>
                            <p>Code lint report: </p>
                            <pre>${codeLintReport}</pre>
                            </body></html>
                          """
            }
        }
        always {
            // Don't delete 'node_modules' to speed-up builds
            sh "rm -f ${archivedArtifact} || true"
            sh "rm -rf dist || true"
            sh "rm -f ${securityAuditFile} || true"
            sh "rm -f ${codeLintReportFile} || true"
        }
    }
}
