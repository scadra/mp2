def groupId = 'com.luxhub.agora'
def groupPath = groupId.replace('.', '/')
def artifactId = 'marketplace-front'
def mvnCoordinates = "${groupId}:${artifactId}:${version}:bin:zip"
def artifactFile = "${artifactId}-${version}-bin.zip"
def snapshotVersion = params.version.endsWith('-SNAPSHOT')
def nexusId = snapshotVersion ? 'nexus-snapshots' : 'nexus-releases'
def nexusUrl = snapshotVersion
    ? 'https://nexus.luxhub.local:9443/repository/maven-snapshots'
    : 'https://nexus.luxhub.local:9443/repository/maven-releases'
def actualLocation = ''
def mvnCmd = 'mvn -s /opt/jenkins/home/maven-settings.xml -B' // TODO replace -B by --no-transfer-progress on Maven 3.6.1+

pipeline {

    agent {
        label 'master'
    }

    options {
        disableConcurrentBuilds()
        buildDiscarder(logRotator(numToKeepStr: '10'))
    }

    parameters {
        string(name: 'branch', defaultValue: 'master', description: 'Branch to build and release')
        string(name: 'version', defaultValue: '0.0.1-SNAPSHOT', description: 'Artifact version to publish to Nexus')
        choice(name: 'deploy', choices: ['', 'port1-dev.luxhub.local'], description: 'Server where to deploy')
    }

    stages {
        stage('Dependencies') {
            tools {
                nodejs "nodejs-14.15.0"
            }
            options {
                timeout(time: 20, unit: 'MINUTES')
            }
            steps {
                sh 'git status'
                sh 'node -v'
                sh 'npm --version'
                sh 'npm install'
            }
        }

        stage('Build') {
            tools {
                nodejs "nodejs-14.15.0"
            }
            options {
                timeout(time: 10, unit: 'MINUTES')
            }
            steps {
                sh 'npm run build'
                zip dir: 'dist', zipFile: "${artifactFile}", archive: true
                echo "Artifact built and archived: ${artifactFile}"
            }
        }

        // TODO should fail completely if building a release version (non-snapshot)
        stage('QA') {
            options {
                timeout(time: 15, unit: 'MINUTES')
            }
            tools {
                nodejs "nodejs-14.15.0"
            }
            steps {
                echo 'Dependency check for vulnerabilities'
                catchError(buildResult: 'UNSTABLE', stageResult: 'FAILURE') {
                    sh 'npm audit --audit-level=high'
                }
                echo 'Source code quality check'
                catchError(buildResult: 'UNSTABLE', stageResult: 'FAILURE') {
                    sh 'npm run lint'
                }
                echo 'Unit tests with Jest'
                catchError(buildResult: 'UNSTABLE', stageResult: 'FAILURE') {
                    sh 'npm run test'
                }
            }
        }

        stage('Publish') {
            tools {
                maven 'maven-3.6.0'
            }
            steps {
                echo "Publishing to ${nexusId}"
                script {
                    def packageJson = readJSON file: 'package.json'
                    echo packageJson.name
                    echo packageJson.version
                    echo packageJson.description
                    sh "${mvnCmd} deploy:deploy-file -Dfile=${artifactFile} -DrepositoryId=${nexusId} -Durl=${nexusUrl} -DgroupId=${groupId} -DartifactId=${artifactId} -Dclassifier=bin -Dversion=${version} -Ddescription='${packageJson.description}'"
                    def actualVersion = snapshotVersion
                            ? sh (script: "curl -k ${nexusUrl}/${groupPath}/${artifactId}/${version}/maven-metadata.xml | grep '<value>' | head -1 | sed 's/<value>\\(.*\\)<\\/value>/\\1/'", returnStdout: true).trim()
                            : version;
                    actualLocation = "${nexusUrl}/${groupPath}/${artifactId}/${version}/${artifactId}-${actualVersion}-bin.zip"
                }
                echo "Artifact ${mvnCoordinates} published to ${actualLocation}"
            }
        }

        stage('Deploy') {
            when {
                expression { params.deploy != '' }
            }
            steps {
                echo "Executing deploy job to target ${params.deploy}"
                build job: 'Marketplace-Frontend-Deploy',
                    parameters: [
                        string(name: 'version', value: params.version),
                        string(name: 'target', value: params.deploy)
                    ]
            }
        }
    }

    post {
        always {
            deleteDir()
            script {
                if (!params.deploy && (currentBuild.currentResult == 'SUCCESS' || currentBuild.currentResult == 'UNSTABLE')) {
                    mail subject: "${artifactFile} published to ${nexusId}",
                            from: 'Jenkins <jenkins@luxhub.com>',
                            to: 'patrice.jacquot@luxhub.com',
                            mimeType: 'text/html',
                            body: """
                        <html><body>
                        <h1>${currentBuild.currentResult}: ${env.JOB_BASE_NAME}</h1>
                        <h2>Suggested action</h2>
                        <p>${currentBuild.currentResult == 'SUCCESS' ? 'None, this message is for information only.' : '<a href="' + env.BUILD_URL + 'console">Check job execution log</a> and fix build issues.'}</p>
                        <h2>Execution</h2>
                        <p>
                            <a href="${env.BUILD_URL}">Build <strong>#${env.BUILD_NUMBER}</strong></a> of the job <strong>${env.JOB_NAME}</strong>
                            ran on the branch <strong>${params.branch}</strong>.
                        </p>
                        <h2>Outcome</h2>
                        <p>
                            Artifact <a href="${actualLocation}"><strong>${mvnCoordinates}</a></strong> has been published to <a href="${nexusUrl}">${nexusId}</a>.
                        </p>
                        </body></html>
                      """
                }
            }
        }
    }
}
