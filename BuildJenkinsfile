def groupId = 'com.luxhub.agora'
def groupPath = groupId.replace('.', '/')
def artifactId = 'marketplace-front'
def version = params['release.version']
def mvnCoordinates = "${groupId}:${artifactId}:${version}:bin:zip"
def artifactFile = "${artifactId}-${version}-bin.zip"
def snapshotVersion = version.endsWith('-SNAPSHOT')
def nexusId = snapshotVersion ? 'nexus-snapshots' : 'nexus-releases'
def nexusUrl = snapshotVersion
    ? 'https://nexus.luxhub.local:9443/repository/maven-snapshots'
    : 'https://nexus.luxhub.local:9443/repository/maven-releases'
def actualLocation = ''

pipeline {

    agent {
        label 'master'
    }

    options {
        disableConcurrentBuilds()
        buildDiscarder(logRotator(numToKeepStr: '10'))
    }

    parameters {
        string(name: 'git.branch', defaultValue: 'master', description: 'Branch from where to build and release')
        string(name: 'release.version', defaultValue: '0.0.1-SNAPSHOT', description: '0.0.1')
    }

    stages {
        stage('Dependencies') {
            tools {
                nodejs "nodejs-14.15.0"
            }
            options {
                timeout(time: 20, unit: 'MINUTES')
            }
            steps {
                sh 'git status'
                sh 'node -v'
                sh 'npm --version'
                sh 'npm install'
            }
        }

        stage('Build') {
            tools {
                nodejs "nodejs-14.15.0"
            }
            options {
                timeout(time: 10, unit: 'MINUTES')
            }
            steps {
                sh 'npm run build'
                zip dir: 'dist', zipFile: "${artifactFile}", archive: true
                echo "Artifact built and archived: ${artifactFile}"
            }
        }

        // TODO should fail completely if building a release version (non-snapshot)
        stage('QA') {
            options {
                timeout(time: 15, unit: 'MINUTES')
            }
            tools {
                nodejs "nodejs-14.15.0"
            }
            steps {
                echo 'Dependency check for vulnerabilities'
                catchError(buildResult: 'UNSTABLE', stageResult: 'FAILURE') {
                    sh 'npm audit --audit-level=high'
                }
                echo 'Source code quality check'
                catchError(buildResult: 'UNSTABLE', stageResult: 'FAILURE') {
                    sh 'npm run lint'
                }
                echo 'Unit tests with Jest'
                catchError(buildResult: 'UNSTABLE', stageResult: 'FAILURE') {
                    sh 'npm run test'
                }
            }
        }

        stage('Publish') {
            tools {
                maven 'maven-3.6.0'
            }
            steps {
                echo "Publishing to ${nexusId}"
                script {
                    def packageJson = readJSON file: 'package.json'
                    echo packageJson.name
                    echo packageJson.version
                    echo packageJson.description
                    sh "mvn -s /opt/jenkins/home/maven-settings.xml deploy:deploy-file -Dfile=${artifactFile} -DrepositoryId=${nexusId} -Durl=${nexusUrl} -DgroupId=${groupId} -DartifactId=${artifactId} -Dclassifier=bin -Dversion=${params['release.version']} -Ddescription='${packageJson.description}' --batch-mode"
                    def actualVersion = snapshotVersion
                            ? sh (script: "curl -k ${nexusUrl}/${groupPath}/${artifactId}/${version}/maven-metadata.xml | grep '<value>' | head -1 | sed 's/<value>\\(.*\\)<\\/value>/\\1/'", returnStdout: true).trim()
                            : version;
                    actualLocation = "${nexusUrl}/${groupPath}/${artifactId}/${version}/${artifactId}-${actualVersion}-bin.zip"
                }
                echo "Artifact ${mvnCoordinates} published to ${actualLocation}"
            }
        }

        /*stage('Deploy') {
            when {
                expression { params.deploy }
            }
            steps {
                echo "Deploying to ${params.deployTarget}"
            }
        }*/
    }

    post {
        success {
            mail subject: "${artifactFile} published to Nexus",
                    from: 'Jenkins <jenkins@luxhub.com>',
                    to: 'patrice.jacquot@luxhub.com',
                    mimeType: 'text/html',
                    body: """
                        <html><body>
                        <h1>${currentBuild.currentResult}: ${env.JOB_BASE_NAME}</h1>
                        <h2>Execution</h2>
                        <p>
                            <a href="${env.BUILD_URL}">Build <strong>#${env.BUILD_NUMBER}</strong></a> of the job <strong>${env.JOB_NAME}</strong>
                            ran on the branch <strong>${params['git.branch']}</strong>.
                        </p>
                        <h2>Outcome</h2>
                        <p>
                            Artifact <a href="${actualLocation}"><strong>${mvnCoordinates}</a></strong> has been published to <strong>${nexusId}</strong>.
                        </p>
                        </body></html>
                      """
        }
        always {
            deleteDir()
        }
    }
}
